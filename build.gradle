allprojects {
    group = "io.github.smileboy0014"
    version = "0.2.0"

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: "java"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Javadoc).configureEach {
        options.encoding = 'UTF-8'
    }
}

/**
 * Central Portal Publishing API를 사용한 배포 태스크
 * https://central.sonatype.com/api/v1/publisher/upload
 */
tasks.register('publishToCentralPortal') {
    group = 'publishing'
    description = 'Publishes all publications to Central Portal using Publishing API'

    dependsOn ':concur-spec-core:publishMavenJavaPublicationToMavenLocal'

    doLast {
        def username = (findProperty('ossrhUsername') ?: System.getenv('OSSRH_USERNAME'))?.toString()
        def password = (findProperty('ossrhPassword') ?: System.getenv('OSSRH_PASSWORD'))?.toString()

        if (!username || !password) {
            throw new GradleException("OSSRH credentials not found")
        }

        def group = project.group.toString().replace('.', '/')
        def artifactId = 'concur-spec-core'
        def version = project.version.toString()

        // Maven Local 경로에서 파일 수집
        def mavenLocal = new File(System.getProperty('user.home'), '.m2/repository')
        def artifactDir = new File(mavenLocal, "${group}/${artifactId}/${version}")

        if (!artifactDir.exists()) {
            throw new GradleException("Artifact directory not found: ${artifactDir}")
        }

        // ZIP 번들 생성 (flat structure 필요)
        def bundleFile = new File(buildDir, "central-bundle.zip")
        bundleFile.parentFile.mkdirs()

        // 체크섬 파일 생성
        artifactDir.listFiles().each { file ->
            if (file.isFile() && !file.name.endsWith('.md5') && !file.name.endsWith('.sha1')) {
                // MD5
                def md5File = new File(artifactDir, "${file.name}.md5")
                if (!md5File.exists()) {
                    def md5 = java.security.MessageDigest.getInstance('MD5')
                    file.withInputStream { stream -> md5.update(stream.bytes) }
                    md5File.text = md5.digest().collect { String.format('%02x', it) }.join('')
                }
                // SHA1
                def sha1File = new File(artifactDir, "${file.name}.sha1")
                if (!sha1File.exists()) {
                    def sha1 = java.security.MessageDigest.getInstance('SHA-1')
                    file.withInputStream { stream -> sha1.update(stream.bytes) }
                    sha1File.text = sha1.digest().collect { String.format('%02x', it) }.join('')
                }
            }
        }

        // Maven 표준 디렉토리 구조로 ZIP 생성
        // 경로: io/github/smileboy0014/concur-spec-core/0.1.0/
        def zipPath = "${group}/${artifactId}/${version}/"

        ant.zip(destfile: bundleFile) {
            artifactDir.listFiles().each { file ->
                if (file.isFile()) {
                    zipfileset(file: file.absolutePath, fullpath: "${zipPath}${file.name}")
                }
            }
        }

        println "Created bundle: ${bundleFile} (${bundleFile.length()} bytes)"

        // Central Portal에 업로드
        def deploymentName = "${artifactId}-${version}"
        def apiUrl = "https://central.sonatype.com/api/v1/publisher/upload?name=${deploymentName}&publishingType=AUTOMATIC"

        println "Uploading to Central Portal..."

        // Central Portal API uses Basic Authentication with username:password
        def curlCommand = [
            'curl', '-X', 'POST',
            '-f',  // Fail on HTTP errors
            '-u', "${username}:${password}".toString(),  // Basic Auth
            '-F', "bundle=@${bundleFile.absolutePath}".toString(),
            apiUrl.toString()
        ]

        def process = new ProcessBuilder(curlCommand as List<String>)
            .redirectErrorStream(true)
            .start()

        def output = new StringBuilder()
        process.inputStream.eachLine { line ->
            output.append(line).append('\n')
            println line
        }

        def exitCode = process.waitFor()

        if (exitCode != 0 || output.toString().contains('"error"')) {
            throw new GradleException("Failed to upload to Central Portal. Exit code: ${exitCode}\nResponse: ${output}")
        }

        println "Successfully published to Central Portal"
    }
}
